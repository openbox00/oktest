/*
 * The following defines were originally in core kernel code.  They are
 * repeated here as SoC code should really handle its own definitions of this
 * info.
 */
#define CP15             p15
#define C15_control      c1
#define C15_CONTROL_INIT 0x0078

#define STACK_TOP       ((1 << 10) - 8)

/* This is the entry point of the NICTA::Pistachio ARM architecture.
 * The boot loader must call _start with the processor in privileged
 * mode and translation disabled.
 */
        .section .init.head
        .global  __phys_addr_stack                     
	    .global _start;                               
    	.align;                                   
_start:
        /* Setup status register for supervisor mode, interrupts disabled */
        msr     cpsr_fc, #0xd3

        /* Setup the CP15 control register to a known initial state */
        ldr     r0,     =C15_CONTROL_INIT
        mcr     CP15, 0, r0, C15_control, c0

L0:
        mrc     p15, 0, r15, c7, c14, 3
        bne     L0

        /* Drain write buffer. */
        mov    r0, #0
        mcr     p15, 0, r0, c7, c10, 4

        /* Flush I-Cache */
        mov    r0, #0
        mcr     p15, 0, r0, c7, c5, 0

        /* flush I+D TLB */
        mov    r0, #0
        mcr     p15, 0, r0, c8, c7, 0

        /* Setup boot stack (physical address) */
        ldr     sp,     __phys_addr_stack
        add     sp,     sp,     #STACK_TOP

        /* Jump to startup_system */
        bl      kernel_arch_init

        /* NOT REACHED */
L1:
        b       L1

        /* These need to be filled in by the build system */
__phys_addr_stack:
        .word    0x0

