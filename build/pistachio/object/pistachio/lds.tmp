ENTRY(_start)

MEMORY
{
    ram(RWX) : ORIGIN = 0xF0000000, LENGTH = 0x10000000
}



SECTIONS
{


    .text :
    {
        _start_rom = .;

        . = ALIGN(64K);
        *(.init.head)
        *(.init.start)
        *(.base)
        *(.text.startup)
        *(.text)
        *(.gnu.linkonce.*)
    } > ram

    .rodata . :
    {
        *(.rodata)
        *(.rodata.*)
    } > ram

    .kdebug . :
    {
        *(.kdebug)
    } > ram

    _end_rom = .;

    _start_init = .;
    .init . :
    {
        *(.init)
        *(.init.*)
    } > ram

    .roinit . :
    {
        *(.roinit)
    } > ram
    .elfweaver_info :
    {
        *(.elfweaver_info)
    } > ram
    _end_init = .;


    .data :
    {

        _start_ram = .;
        _start_data = .;
        *(.data.cpulocal)
        *(.data.cpulocal.stack)
        *(.data.cpulocal.utcb)
        *(.data.cpulocal.tcb)
        *(.sdata)
        *(.data)

    } > ram

    .kdebug-data . :
    {
        *(.data.kdebug)
        *(.kdebug.data)
    } > ram

    .bss . :
    {
        . = ALIGN(16);
        _start_setlist = .;
        *(.setlist)
        _end_setlist = .;
        . = ALIGN(16);
        _start_sets = .;
        *(SORT(set_*))
        _end_sets = .;
 . = ALIGN(32);
        *(.bss)
        *(.kdebug-bss)
    } > ram

    . = ALIGN(16K);
    .kspace . :
    {
        *(.data.kspace)
    } > ram
    . = ALIGN(4K);
    .traps . :
    {
        *(.data.traps)
    } > ram

    .utcb_page . :
    {
        *(.data.utcb)
    } > ram

    . = ALIGN(4K);
    _end_ram = .;






    . = ALIGN(4K);
    _end = .;

    /DISCARD/ :
    {

        *(.eh_frame)
        *(.note)
        *(.comment)
        *(.delete)
    }
}

ASSERT(SIZEOF(.traps) <= 0x1000, "ERROR: trap section is larger than 4K!!")
